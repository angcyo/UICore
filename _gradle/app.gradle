//ext {
//    V_CODE = "1"
//    V_NAME = "1.0.1"
//    schema = "app"
//
//    application_id = "com.angcyo.app"
//}
//apply from: allGradle.app

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
//apply plugin: 'com.getkeepsafe.dexcount'
//apply from: 'build_time.gradle'

apply from: allGradle.appBase
apply from: allGradle.aar
apply from: allGradle.key

static def apkTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("Asia/Shanghai"))
}

def apk_time = apkTime()

def _C_SDK = project.hasProperty("C_SDK") ? project.ext.C_SDK : '28'
def _M_SDK = project.hasProperty("M_SDK") ? project.ext.M_SDK : '21'
def _T_SDK = project.hasProperty("T_SDK") ? project.ext.T_SDK : '28'
def _V_CODE = project.hasProperty("V_CODE") ? project.ext.V_CODE : '1'
def _V_NAME = project.hasProperty("V_NAME") ? project.ext.V_NAME : '1.0'
def application_id = project.hasProperty("application_id") ? project.ext.application_id : 'com.angcyo.app'
def _app_name = project.hasProperty("APP_NAME") ? project.ext.APP_NAME : ((project.name != "app") ? project.name : rootProject.name)

def _minify_enabled_debug = project.hasProperty("minify_enabled_debug") ? (project.ext.minify_enabled_debug == "true") : false
def _use_proguard_debug = project.hasProperty("use_proguard_debug") ? (project.ext.use_proguard_debug == "true") : false
def _minify_enabled = project.hasProperty("minify_enabled") ? (project.ext.minify_enabled == "true") : _minify_enabled_debug
def _use_proguard = project.hasProperty("use_proguard") ? (project.ext.use_proguard == "true") : _minify_enabled

println "\n........app.gradle........"
println "工程名->" + project.name + " 包名->" + application_id + " ->" + _V_NAME + " ->" + _V_CODE
println "C_SDK->" + _C_SDK + " M_SDK->" + _M_SDK + " T_SDK->" + _T_SDK + " minify_enabled->" + _minify_enabled + " use_proguard->" + _use_proguard
println "........app.gradle........end"

//println hasProperty("V_NAME")
//println rootProject.hasProperty("V_NAME")
//println project
//println rootProject

android {

    compileSdkVersion Integer.parseInt(_C_SDK)
    //buildToolsVersion rootProject.B_TOOLS
    defaultConfig {
        applicationId "$application_id"
        minSdkVersion Integer.parseInt(_M_SDK)
        targetSdkVersion Integer.parseInt(_T_SDK)
        versionCode Integer.parseInt(_V_CODE)
        versionName "$_V_NAME"
        multiDexEnabled true
        resConfigs "zh", "xhdpi" //, "en"

        buildConfigField "boolean", "SHOW_DEBUG", "false"

        manifestPlaceholders = [
                PACKAGE_NAME: "$application_id"
        ]

//        ndk {
//            // 设置支持的SO库架构
//            abiFilters 'armeabi', 'armeabi-v7a', 'x86', 'x86_64'//, 'arm64-v8a'
//            //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
//        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
//    sourceSets {
//        main {
//            res.srcDirs = [
//                    'src/main/res/'
//            ]
//        }
//    }

    flavorDimensions "flavor"
    productFlavors {
        //develop
        _dev {
            dimension "flavor"
            minSdkVersion Math.max(Integer.parseInt(_M_SDK), 21)
            buildConfigField "boolean", "SHOW_DEBUG", "true"
        }
        //preview
        pre {
            dimension "flavor"
            minSdkVersion Integer.parseInt(_M_SDK)
            buildConfigField "boolean", "SHOW_DEBUG", "true"
        }
        //apk
        apk {
            dimension "flavor"
            minSdkVersion Integer.parseInt(_M_SDK)
        }
    }
    buildTypes {
        release {
            //applicationIdSuffix ".release"
            initWith debug
            minifyEnabled _minify_enabled
            debuggable false
            jniDebuggable = false
            crunchPngs = true
        }
        pretest {
            //applicationIdSuffix ".release"
            initWith debug
            minifyEnabled _minify_enabled
            crunchPngs = true
        }
        debug {
            //applicationIdSuffix ".debug"
            zipAlignEnabled true
            shrinkResources false
            minifyEnabled _minify_enabled_debug
//            useProguard _use_proguard_debug
            signingConfig signingConfigs.angcyo
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable true
            jniDebuggable = true
            crunchPngs = false

            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
        }
    }
    lintOptions {
        abortOnError false
        //https://www.jianshu.com/p/93678f8b3421
        disable 'GoogleAppIndexingWarning'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    /*Gradle3.0 以下的方法*/
//    getApplicationVariants().all { variant ->
//        variant.outputs.each { output ->
//            def appName = "UIViewDemo-${variant.buildType.name}-${variant.versionName}"
//            def time = ""
//            if (variant.buildType.name.equalsIgnoreCase("release")) {
//                time = "_${new Date().format("yyyy-MM-dd_HH-mm")}"
//            }
//            output.outputFile = new File(output.outputFile.parent, "${appName}${time}.apk")
//        }
//    }

//    /*Gradle3.0 以上的方法*/
//    applicationVariants.all { variant ->
//        if (variant.buildType.name != "debug") {
//            variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apk")
//        }
//
//        variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
//            apkData.outputFileName = _app_name + "-" +
//                    variant.versionName + "_" +
//                    apk_time + "_" +
//                    variant.flavorName + "_" +
//                    variant.buildType.name + "_" +
//                    variant.signingConfig.name + "_" +
//                    signConfig.key_alias +
//                    ".apk"
//        }
//    }

    /*Gradle3.3 以上的方法*/
    applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {
            variant.getPackageApplicationProvider().get().outputDirectory = new File(project.rootDir.absolutePath + "/apk")
        }

        variant.getPackageApplicationProvider().get().outputScope.apkDatas.forEach { apkData ->
            apkData.outputFileName = _app_name + "-" +
                    variant.versionName + "_" +
                    apk_time + "_" +
                    variant.flavorName + "_" +
                    variant.buildType.name + "_" +
//                    variant.signingConfig.name + "_" +
                    signConfig.key_alias +
                    ".apk"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(allModule.core)
}

apply from: allGradle.version
apply from: allGradle.collectMR