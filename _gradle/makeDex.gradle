import java.util.regex.Pattern

/**
 * Email:angcyo@126.com
 *
 * 脚本测试环境:
 * com.android.tools.build:gradle:3.5.0
 * gradle-5.4.1-all
 *
 * 要求:
 * 1.正确配置的`java`环境
 *
 * 注意:
 * 如果使用JDK9和以上版本, 出现以下错误:
 *
 * -Djava.ext.dirs=D:\android\sdk\build-tools\29.0.2\lib is not supported.  Use -classpath instead.
 * Error: Could not create the Java Virtual Machine.
 * Error: A fatal exception has occurred. Program will exit.
 *
 * 请参考: https://www.jianshu.com/p/77db2ea8098f
 *
 * 修改方法,将 [dx.bat] 文件的最后一行:
 * call "%java_exe%" %javaOpts% -Djava.ext.dirs="%frameworkdir%" -jar "%jarpath%" %params%
 * 改成:
 * call "%java_exe%" %javaOpts% --class-path="%frameworkdir%" -jar "%jarpath%" %params%
 *
 * @author angcyo* @date 2019/09/06
 * Copyright (c) 2019 ShenZhen O&M Cloud Co., Ltd. All rights reserved.
 */

ext.dexConfig = [

        //需要打包的包名(包含包名下的所有类)
        "includePackageNames": [
                "com/wayto/reservoir/patrol/plugin/fragment/"
        ],
        //需要排除的包名, 也可以是正则表达式
        "excludePackageNames": [
                /* "com/wayto/reservoir/patrol/plugin/fragment/entity/",
                 "com/wayto/reservoir/patrol/plugin/fragment/bean/",
                 ".*Entity.*"*/
        ],
        //文件名 (默认是工程名+时间的组合)
        "dexFileName"        : "dex",
        //当前版本
        "versionCode"        : "2",
        "versionName"        : "v1.0.1",

        //-------------内部使用--------------

        //jar文件输出路径
        "jarPath"            : "",
        //dex文件输出路径
        "dexPath"            : "",
        //dex默认文件后缀
        "dexSuffix"          : ".r.dex"
]

task _makeDex(group: 'angcyo', description: '将jar打包成dex文件') {
    dependsOn("jarToDex")

    doFirst {
        println "开始打包dex文件:$dexConfig.includePackageNames"
    }

    doLast {
        error "打包完成:$dexConfig.dexPath"
    }
}

/**
 * 这里的依赖任务`compileApkReleaseJavaWithJavac`请根据"compile(.*)JavaWithJavac"匹配规则
 * 找到自己工程对应任务名.
 *
 * 此任务执行后, 才会生成kotlin语言对应的 .class 文件.
 * */
task makeJar(dependsOn: ['compileApkReleaseJavaWithJavac'], type: Jar, group: 'angcyo', description: '生成jar文件') {
    //需要打包进jar的包名(此包名下的所有class)
    def includePackageNames = dexConfig.includePackageNames
    def excludePackageNames = dexConfig.excludePackageNames

    //编译类型 风味类型
    def buildFlavors = "apkRelease"
    def buildDir = project.buildDir.getAbsolutePath()

    def javaClassPath = "$buildDir/intermediates/javac/$buildFlavors/classes"
    def kotlinClassPath = "$buildDir/tmp/kotlin-classes/$buildFlavors"

    //jar 输出目录
    destinationDirectory.set(rootProject.file("dex"))

    //名字规则
    //[archiveBaseName]-[archiveAppendix]-[archiveVersion]-[archiveClassifier].[archiveExtension]
    //jar 文件名

    if (dexConfig.dexFileName.isEmpty()) {
        archiveBaseName.set(project.name)
        def time = new Date()
                .format("yyyy-MM-dd_HH-mm", TimeZone.getTimeZone("Asia/Shanghai"))
        archiveAppendix.set(time)
    } else {
        archiveBaseName.set(dexConfig.dexFileName)
    }

    archiveVersion.set(dexConfig.versionName)
    archiveExtension.set("jar")

    //archiveClassifier.set("all")

    //复制文件的源路径
    from kotlinClassPath, javaClassPath
    //需要匹配的规则, 如果未设置, 则全部复制
    //include javaClassPath, kotlinClassPath

    //需要匹配的规则, 如果未设置, 则全部不排除
    //exclude

    include { fileTree ->
//        includePackageNames.any {
//            it.startsWith(fileTree.path)
//        } || includePackageNames.any {
//            fileTree.path.startsWith(it)
//        }

        if (fileTree.isDirectory()) {
            true
        } else {
            def classPath = fileTree.path

            def include = includePackageNames.any {
                classPath.startsWith(it)
            }

            if (include) {
                include = !excludePackageNames.any {
                    classPath.startsWith(it) ||
                            Pattern.compile(it).matcher(classPath).matches()
                }
            }

            if (include) {
                error "包含: $classPath"
            }

            include
        }
    }

    //写入jar文件中的 META-INF/MANIFEST.MF 文件的信息
    manifest {
        attributes["Build-Time"] = new Date()
                .format("yyyy-MM-dd_HH-mm-ss", TimeZone.getTimeZone("Asia/Shanghai"))
        attributes["Author"] = "angcyo"
        attributes["Copyright"] = "Wayto"
        attributes["versionName"] = dexConfig.versionName
        attributes["versionCode"] = dexConfig.versionCode
    }

    dexConfig.jarPath = archiveFile.get().getAsFile().getAbsolutePath()
}

task jarToDex(dependsOn: ['makeJar'], type: Exec, group: 'angcyo', description: '将jar转换成dex文件') {
    //借助windows的cmd命令行执行
    commandLine 'cmd'

    def _local = 'local.properties'
    def _sdk = 'sdk.dir'

    Properties properties = new Properties()
    File local = new File(rootDir, _local)
    if (!local.exists()) {
        throw new IllegalArgumentException("[$_local]配置文件不存在.")
    }

    properties.load(local.newInputStream())

    def sdkDir = properties.getProperty(_sdk)

    if (sdkDir == null) {
        throw new IllegalArgumentException("sdk 路径未找到.请检查[$_local]中的[$_sdk]属性.")
    }

    def dxPath = "$sdkDir/build-tools/$android.buildToolsVersion/dx.bat"

    doFirst {

        //jar文件对象
        def srcFile = file(dexConfig.jarPath)
        //需要生成的dex文件对象
        def desFile = file(srcFile.parent + "/" +
                srcFile.name.substring(0, srcFile.name.lastIndexOf(".")) +
                dexConfig.dexSuffix)

        dexConfig.dexPath = desFile.getAbsolutePath()

        //此行可以不需要
        workingDir srcFile.parent

        //拼接dx.bat执行的参数
        def list = []
        list.add("/c")
        list.add(dxPath)
        list.add("--dex")
        list.add("--output")
        list.add(desFile)
        list.add(dexConfig.jarPath)

        //设置参数到cmd命令行
        args list
    }
}

static def error(Object msg) {
    System.err.println msg
}